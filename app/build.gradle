plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'com.google.devtools.ksp' version '1.9.10-1.0.13'
}

android {
    namespace 'com.parseus.codecinfo'
    compileSdk 34
    defaultConfig {
        applicationId "com.parseus.codecinfo"
        minSdkVersion 21
        targetSdkVersion 34
        versionCode 25
        versionName "2.6.0"
        resConfigs 'en'
    }
    buildTypes {
        debug {
            versionNameSuffix "-dev"
            if (!gradle.startParameter.taskNames.toString().contains('StandardTv')) {
                multiDexEnabled true
            }
        }
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    flavorDimensions = ["app", "platform"]

    productFlavors {
        nonFree {
            dimension "app"

            dependenciesInfo {
                includeInApk = false
            }
        }

        standard {
            dimension "app"

            dependenciesInfo {
                includeInApk = false
            }
        }

        mobile {
            dimension "platform"
            versionCode 10000 + android.defaultConfig.versionCode
        }

        tv {
            dimension "platform"
            versionCode 20000 + android.defaultConfig.versionCode
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    kotlin {
        jvmToolchain(11)
    }
    packagingOptions {
        jniLibs {
            excludes += ['kotlin/**']
        }
        resources {
            excludes += ['kotlin/**', '**/*.kotlin_metadata', 'META-INF/*.kotlin_module',
                         'META-INF/*.version']
        }
    }

    variantFilter { variant ->
        def names = variant.flavors*.name
        if (names.contains("nonFree") && names.contains("tv")) {
            setIgnore(true)
        }
    }
}

configurations {
    nonFreeMobileImplementation {}
    standardMobileImplementation {}
}

dependencies {
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.7.3'

    implementation 'androidx.appcompat:appcompat:1.7.0-alpha03'
    implementation "androidx.core:core-ktx:1.12.0"
    implementation 'androidx.fragment:fragment-ktx:1.7.0-alpha06'
    implementation 'androidx.preference:preference-ktx:1.2.1'
    implementation 'androidx.recyclerview:recyclerview:1.3.1'

    implementation "com.squareup.leakcanary:plumber-android:$leakCanary_version"
    implementation "com.squareup.moshi:moshi:$moshi_version"
    ksp "com.squareup.moshi:moshi-kotlin-codegen:$moshi_version"
    implementation "com.squareup.okio:okio:3.6.0"

    debugImplementation "com.squareup.leakcanary:leakcanary-android:$leakCanary_version"

    tvImplementation 'androidx.leanback:leanback:1.2.0-alpha03'
    tvImplementation 'androidx.leanback:leanback-preference:1.2.0-alpha03'

    standardMobileImplementation 'org.lsposed.hiddenapibypass:hiddenapibypass:4.3'

    mobileImplementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    mobileImplementation 'androidx.core:core-splashscreen:1.1.0-alpha02'
    mobileImplementation 'androidx.palette:palette-ktx:1.0.0'
    mobileImplementation 'androidx.webkit:webkit:1.8.0'
    mobileImplementation 'com.github.KieronQuinn:MonetCompat:0.4.1'
    mobileImplementation 'com.github.marcoscgdev:Licenser:2.0.0'
    mobileImplementation 'com.google.android.material:material:1.11.0-alpha03'

    nonFreeMobileImplementation 'com.google.android.play:app-update:2.1.0'
    nonFreeMobileImplementation 'com.google.android.play:app-update-ktx:2.1.0'
    nonFreeMobileImplementation 'com.google.android.play:review:2.0.1'
    nonFreeMobileImplementation 'com.google.android.play:review-ktx:2.0.1'
}